apply plugin: 'maven'

// 全局的Properties
Properties globalProperties = new Properties()
globalProperties.load(
        project.rootProject.file('gradle.properties').newDataInputStream())
// 读取全局通用属性值
def VERSION_NAME = globalProperties.getProperty("VERSION_NAME")
def POM_URL = globalProperties.getProperty("POM_URL")
def GROUP_ID = globalProperties.getProperty("GROUP_ID")

Properties projectProperties = new Properties()
// 拿到每一个项目中的 gradle.properties
projectProperties.load(
        project.file('gradle.properties').newDataInputStream())
def POM_ARTIFACT_ID = projectProperties.getProperty("POM_ARTIFACT_ID")

println("VERSION_NAME = $VERSION_NAME")
println("POM_URL = $POM_URL")
println("GROUP_ID = $GROUP_ID")
println("POM_ARTIFACT_ID = $POM_ARTIFACT_ID")

uploadArchives {
    repositories {
        mavenDeployer {
            // 填入发布信息
            repository(url: uri(POM_URL)) {
                pom.groupId = GROUP_ID
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME
            }

            // aar之间是能相互之间依赖的,修正发布的版本号
            pom.whenConfigured { pom ->
                pom.dependencies.forEach { dep ->
                    if (dep.getVersion() == "unspecified") {
                        dep.setGroupId(GROUP_ID)
                        dep.setVersion(VERSION_NAME)
                    }
                }
            }
        }
    }
}